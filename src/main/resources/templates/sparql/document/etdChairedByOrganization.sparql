PREFIX tamu: <http://vivo.library.tamu.edu/ontology/TAMU#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?organization rdfs:label ?nameWithId .
} WHERE {
    <[(${uri})]> tamu:ETDChairedBy ?chair .
    ?chair vivo:relatedBy ?position .
    ?position a vivo:Position .
    ?position vivo:relatedBy ?organization .
    ?organization a foaf:Organization .
    ?organization rdfs:label ?name .
    ?organization vitro:mostSpecificType ?mostSpecificType .
    BIND( CONCAT( STR(?name), REPLACE(STR(?chair), "(^.*/)", "::"), REPLACE(STR(?organization), "(^.*/)", "::") ) AS ?nameWithId ) .
    FILTER(lang(?name) = '' && ?mostSpecificType = vivo:AcademicDepartment)
}